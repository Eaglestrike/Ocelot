plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "jaci.openrio.gradle.GradleRIO" version "2018.03.06"
    id "net.ltgt.errorprone" version "0.0.13"
}

sourceCompatibility = 1.8

repositories {
    jcenter()
}

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", jaci.openrio.gradle.frc.RoboRIO) {
            team = 114
        }
    }
    artifacts {
        artifact("frcJava", jaci.openrio.gradle.frc.FRCJavaArtifact) {
            targets << "roborio"
            if (project.hasProperty("debug")) {
                project.logger.lifecycle("\\ Debugging has been enabled!")
                project.logger.lifecycle("\\ When deployed, will wait for remote debugger before launching robot code.")
                debug = true
            }
        }
    }
}

dependencies {
    compile wpilib()
    compile navx()
    compile ctre()
    compile openrio.powerup.matchData()

    compile group: 'org.ejml', name: 'ejml-all', version: '0.33'

    compile 'com.google.code.gson:gson:2.8.2'

    // nullable annotations
    compile(
            ['com.google.code.findbugs:jsr305:2.0.1'],
            ['com.google.code.findbugs:annotations:2.0.1']
    )

    // testing and mocking
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'

    testCompile "org.mockito:mockito-core:2.+"
}

test {
    useJUnitPlatform()
    // tell gradle to always rerun tests
    outputs.upToDateWhen { false }
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest jaci.openrio.gradle.GradleRIOPlugin.javaManifest("org.team114.ocelot.Robot")
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.6"
}
